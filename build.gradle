plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.15.0'
}
pitest {
    pitestVersion = '1.16.0'
    junit5PluginVersion = '1.2.0'

    //  실제 테스트 대상 (서비스/도메인/비즈니스 로직)
    targetClasses = [
            'dev.woori.wooriLearn.domain.*.*',
            'dev.woori.wooriLearn.config.jwt.*',
            'dev.woori.wooriLearn.config.filter.*'
    ]

    // 돌연변이 제외 (Controller/Config/Entity 등 의미 없는 대상들)
    excludedClasses = [
            '*Application',
            '*Config',
            '*Controller',
            '*Dto',
            '*Request',
            '*Response',
            '*Exception',
            '*ExceptionHandler',
            '*entity*',
            '*Repository',
            '*Interceptor',
            '*Adapter',
            '*cache*'
//            'dev.woori.wooriLearn.domain.scenario.entity.*',
//            'dev.woori.wooriLearn.domain.account.entity.*',
//            'dev.woori.wooriLearn.domain.edubankapi.autopayment.entity',
//            'dev.woori.wooriLearn.domain.user.entity.*'
    ]

    // PIT 1.16에서는 'STRONG' 그룹이 제거되어 'STRONGER' 등 지원 그룹으로 지정해야 함
    mutators = ['STRONGER']   // 가장 공격적인 돌연변이 생성 → 테스트 품질↑

    threads = 4
    timestampedReports = false
    outputFormats = ['HTML']

    // 외부 호출로 테스트 터지는 문제 방지
    avoidCallsTo = [
            'java.util.logging',
            'org.slf4j',
            'org.apache.logging',
            'javax.net',
            'org.springframework.web.client.RestTemplate',
            'reactor.core.publisher',
            'org.springframework.data.redis'
    ]

    // Jacoco와 충돌 방지 → 여기 중요!
    useClasspathFile = true
}
group = 'dev.woori'
version = '0.0.1-SNAPSHOT'
description = 'wooriLearn'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'com.mysql:mysql-connector-j'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'


	// Redis Cache
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-cache'

	// Rate Limiting
	implementation 'com.bucket4j:bucket4j-core:8.10.1'
	implementation 'com.bucket4j:bucket4j-redis:8.10.1'

	implementation 'io.jsonwebtoken:jjwt-api:0.11.5' // JWT
	implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // QueryDSL
    implementation "com.querydsl:querydsl-jpa:5.0.0:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
    annotationProcessor "org.hibernate.orm:hibernate-jpamodelgen:6.6.1.Final"

    // Redis Cache
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-cache'

    // Rate Limiting
    implementation 'com.bucket4j:bucket4j-core:8.10.1'
    implementation 'com.bucket4j:bucket4j-redis:8.10.1'
    runtimeOnly 'com.h2database:h2'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.mockito:mockito-junit-jupiter'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test // 테스트 실행 후 리포트 생성

    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
    // 리포트에서 제외할 파일들
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/config/**',           // 설정 파일
                    '**/dto/*Response*',      // Response DTO만 제외
                    '**/entity/**',           // Entity
                    '**/exception/**',        // Exception
                    '**/*Application.class',  // 메인 클래스
                    '**/response/**',          // 공통 응답
                    '**/adapter'
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80 // 80% 이상
            }
        }

        rule {
            element = 'METHOD'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.70// 70% 이상
            }
        }
    }
}
// QueryDSL
def querydslDir = "${buildDir}/generated/sources/annotationProcessor/java/main"

sourceSets {
    main {
        java {
            srcDirs += [ querydslDir ]
        }
    }
}

tasks.withType(JavaCompile) {
    options.annotationProcessorGeneratedSourcesDirectory = file(querydslDir)
}

clean.doLast {
    file(querydslDir).deleteDir()
}

